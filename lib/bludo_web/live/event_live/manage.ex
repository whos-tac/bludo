defmodule BludoWeb.EventLive.Manage do
  use BludoWeb, :live_view

  alias Bludo.{Embeds, Forms, Polls, Presentations, Quizzes}
  alias BludoWeb.Presence

  @impl true
  def mount(%{"code" => code}, session, socket) do
    with %{"locale" => locale} <- session do
      Gettext.put_locale(BludoWeb.Gettext, locale)
    end

    event =
      Bludo.Events.get_event_with_code(code, [
        :user,
        :lti_resource,
        presentation_file: [:polls, :presentation_state]
      ])

    if is_nil(event) || not leader?(socket, event) do
      {:ok,
       socket
       |> put_flash(:error, gettext("Event doesn't exist"))
       |> redirect(to: "/")}
    else
      if connected?(socket) do
        Bludo.Events.Event.subscribe(event.uuid)
        Bludo.Presentations.subscribe(event.presentation_file.id)
      end

      posts = list_all_posts(socket, event.uuid)
      pinned_posts = list_pinned_posts(socket, event.uuid)
      questions = list_all_questions(socket, event.uuid)
      form_submits = list_form_submits(socket, event.presentation_file.id)

      socket =
        socket
        |> assign(:interaction_modal, false)
        |> assign(:settings_modal, false)
        |> assign(:attendees_nb, 1)
        |> assign(:event, event)
        |> assign(:sort_questions_by, "date")
        |> assign(:state, event.presentation_file.presentation_state)
        |> stream(:posts, posts)
        |> stream(:questions, questions)
        |> stream(:pinned_posts, pinned_posts)
        |> stream(:form_submits, form_submits)
        |> assign(:pinned_post_count, length(pinned_posts))
        |> assign(:question_count, length(questions))
        |> assign(:post_count, length(posts))
        |> assign(
          :total_interactions,
          Bludo.Interactions.get_number_total_interactions(event.presentation_file.id)
        )
        |> assign(
          :form_submit_count,
          length(form_submits)
        )
        |> assign(:create, nil)
        |> assign(:list_tab, :posts)
        |> assign(:create_action, :new)
        |> assign(:preview, true)
        |> assign(:is_leader, true)
        |> assign(:attendee_identifier, nil)
        |> assign(:leaders, Bludo.Events.get_activity_leaders_for_event(event.id))
        |> assign(:quiz, %Bludo.Quizzes.Quiz{})
        |> assign(:poll, %Bludo.Polls.Poll{})
        |> assign(:form, %Bludo.Forms.Form{})
        |> push_event("page-manage", %{
          current_page: event.presentation_file.presentation_state.position,
          timeout: 500
        })
        |> interactions_at_position(event.presentation_file.presentation_state.position)

      {:ok, socket}
    end
  end

  defp leader?(%{assigns: %{current_user: current_user}} = _socket, event) do
    Bludo.Events.led_by?(current_user.email, event) || event.user.id == current_user.id
  end

  defp leader?(_socket, _event), do: false

  defp event_id(%{assigns: %{event: event}}), do: event.id

  # --- HANDLE_INFO CLAUSES ---

  @impl true
  def handle_info(%{event: "presence_diff"}, %{assigns: %{event: event}} = socket) do
    attendees = Presence.list("event:#{event.uuid}")
    {:noreply, push_event(socket, "update-attendees", %{count: Enum.count(attendees)})}
  end

  @impl true
  def handle_info({:post_created, post}, socket) do
    socket =
      socket
      |> stream_insert(:posts, post)
      |> update(:post_count, fn post_count -> post_count + 1 end)

    case BludoWeb.Helpers.body_without_links(post.body) =~ "?" do
      true ->
        {:noreply,
         socket
         |> stream_insert(:questions, post)
         |> update(:question_count, fn question_count -> question_count + 1 end)
         |> push_event("scroll", %{})}

      _ ->
        {:noreply, socket |> push_event("scroll", %{})}
    end
  end

  @impl true
  def handle_info({:post_updated, updated_post}, socket) do
    {:noreply,
     socket
     |> stream_insert(:posts, updated_post)
     |> then(fn socket ->
       sorted_questions =
         list_all_questions(socket, socket.assigns.event.uuid, socket.assigns.sort_questions_by)

       stream(socket, :questions, sorted_questions, reset: true)
     end)
     |> stream_insert(:pinned_posts, updated_post)}
  end

  @impl true
  def handle_info({:post_deleted, deleted_post}, socket) do
    socket =
      socket
      |> stream_delete(:posts, deleted_post)
      |> stream_delete(:pinned_posts, deleted_post)
      |> update(:pinned_post_count, fn pinned_post_count ->
        pinned_post_count - if deleted_post.pinned, do: 1, else: 0
      end)
      |> update(:post_count, fn post_count -> post_count - 1 end)

    case BludoWeb.Helpers.body_without_links(deleted_post.body) =~ "?" do
      true ->
        {:noreply,
         socket
         |> stream_delete(:questions, deleted_post)
         |> update(:question_count, fn question_count -> question_count - 1 end)}

      _ ->
        {:noreply, socket}
    end
  end

  @impl true
  def handle_info({:post_pinned, post}, socket) do
    updated_socket =
      socket
      |> stream_insert(:posts, post)
      |> stream_insert(:pinned_posts, post)
      |> stream_insert(:questions, post)
      |> assign(:pinned_post_count, socket.assigns.pinned_post_count + 1)

    {:noreply, updated_socket}
  end

  @impl true
  def handle_info({:post_unpinned, post}, socket) do
    updated_socket =
      socket
      |> stream_insert(:posts, post)
      |> stream_delete(:pinned_posts, post)
      |> stream_insert(:questions, post)
      |> assign(:pinned_post_count, socket.assigns.pinned_post_count - 1)

    {:noreply, updated_socket}
  end

  @impl true
  def handle_info({:form_submit_created, fs}, socket) do
    {:noreply,
     socket
     |> stream_insert(:form_submits, fs)
     |> update(:form_submit_count, fn form_submit_count -> form_submit_count + 1 end)
     |> push_event("scroll", %{})}
  end

  @impl true
  def handle_info({:form_submit_updated, fs}, socket) do
    {:noreply, socket |> stream_insert(:form_submits, fs)}
  end

  @impl true
  def handle_info({:form_submit_deleted, fs}, socket) do
    {:noreply,
     socket
     |> stream_delete(:form_submits, fs)
     |> update(:form_submit_count, fn form_submit_count -> form_submit_count - 1 end)}
  end

  @impl true
  def handle_info({:poll_created, poll}, socket) do
    {:noreply, interactions_at_position(socket, poll.position)}
  end

  @impl true
  def handle_info({:form_created, form}, socket) do
    {:noreply, interactions_at_position(socket, form.position)}
  end

  @impl true
  def handle_info({:embed_created, embed}, socket) do
    {:noreply, interactions_at_position(socket, embed.position)}
  end

  @impl true
  def handle_info({:quiz_created, quiz}, socket) do
    {:noreply, interactions_at_position(socket, quiz.position)}
  end

  @impl true
  def handle_info({:poll_updated, poll}, socket) do
    {:noreply, interactions_at_position(socket, poll.position)}
  end

  @impl true
  def handle_info({:embed_updated, embed}, socket) do
    {:noreply, interactions_at_position(socket, embed.position)}
  end

  @impl true
  def handle_info({:form_updated, form}, socket) do
    {:noreply, interactions_at_position(socket, form.position)}
  end

  @impl true
  def handle_info({:quiz_updated, quiz}, socket) do
    {:noreply, interactions_at_position(socket, quiz.position)}
  end

  @impl true
  def handle_info({:poll_deleted, poll}, socket) do
    {:noreply, interactions_at_position(socket, poll.position)}
  end

  @impl true
  def handle_info({:embed_deleted, embed}, socket) do
    {:noreply, interactions_at_position(socket, embed.position)}
  end

  @impl true
  def handle_info({:form_deleted, form}, socket) do
    {:noreply, interactions_at_position(socket, form.position)}
  end

  @impl true
  def handle_info({:quiz_deleted, quiz}, socket) do
    {:noreply, interactions_at_position(socket, quiz.position)}
  end

  @impl true
  def handle_info({:state_updated, state}, socket) do
    if state.position != socket.assigns.state.position do
      {:noreply,
       socket
       |> assign(:state, state)
       |> interactions_at_position(state.position)
       |> push_event("page-manage", %{current_page: state.position, timeout: 0})}
    else
      {:noreply, socket |> assign(:state, state)}
    end
  end

  @impl true
  def handle_info({:page_changed, page}, socket) do
    # When page changes globally, update local state if it differs
    if socket.assigns.state.position != page do
      {:noreply,
       socket
       |> assign(:state, %{socket.assigns.state | position: page})
       |> interactions_at_position(page)
       |> push_event("page-manage", %{current_page: page, timeout: 0})}
    else
      {:noreply, socket}
    end
  end

  @impl true
  def handle_info({:current_interaction, interaction}, socket) do
    if socket.assigns.current_interaction != interaction do
      position = if interaction, do: interaction.position, else: socket.assigns.state.position

      {:noreply,
       socket
       |> assign(:current_interaction, interaction)
       |> interactions_at_position(position)}
    else
      {:noreply, socket}
    end
  end

  @impl true
  def handle_info({:new_post, post}, socket) do
    socket =
      if post.pinned do
        socket
        |> stream_insert(:pinned_posts, post, at: 0)
        |> assign(:pinned_post_count, socket.assigns.pinned_post_count + 1)
      else
        socket
        |> stream_insert(:posts, post, at: 0)
        |> assign(:post_count, socket.assigns.post_count + 1)
      end

    {:noreply, socket}
  end

  @impl true
  def handle_info({:new_form_submit, _submit}, socket) do
    # Refresh interactions to update response counts
    {:noreply, interactions_at_position(socket, socket.assigns.state.position)}
  end

  @impl true
  def handle_info(_, socket) do
    {:noreply, socket}
  end

  # --- HANDLE_EVENT CLAUSES ---

  @impl true
  def handle_event("current-page", %{"page" => page}, socket) do
    page = String.to_integer(page)

    {:ok, new_state} =
      Bludo.Presentations.update_presentation_state(socket.assigns.state, %{
        :position => page
      })

    # Broadcast page change to all subscribers
    Phoenix.PubSub.broadcast(
      Bludo.PubSub,
      "presentation:#{socket.assigns.event.presentation_file.id}",
      {:page_changed, page}
    )

    {:noreply,
     socket
     |> assign(:state, new_state)
     |> interactions_at_position(page, true)
     |> push_event("page-manage", %{current_page: page, timeout: 0})}
  end

  @impl true
  def handle_event("poll-set-active", %{"id" => id}, socket) do
    case Polls.get_poll_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      poll ->
        with :ok <- Bludo.Interactions.enable_interaction(poll) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, poll}
          )

          {:noreply,
           socket
           |> assign(:current_interaction, poll)
           |> interactions_at_position(socket.assigns.state.position)}
        end
    end
  end

  @impl true
  def handle_event("form-set-active", %{"id" => id}, socket) do
    case Forms.get_form_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      form ->
        with :ok <- Bludo.Interactions.enable_interaction(form) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, form}
          )

          {:noreply,
           socket
           |> assign(:current_interaction, form)
           |> interactions_at_position(socket.assigns.state.position)}
        end
    end
  end

  @impl true
  def handle_event("embed-set-active", %{"id" => id}, socket) do
    case Embeds.get_embed_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      embed ->
        with :ok <- Bludo.Interactions.enable_interaction(embed) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, embed}
          )

          {:noreply,
           socket
           |> assign(:current_interaction, embed)
           |> interactions_at_position(socket.assigns.state.position)}
        end
    end
  end

  @impl true
  def handle_event("poll-set-inactive", %{"id" => id}, socket) do
    case Polls.get_poll_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      poll ->
        with {:ok, _} <- Bludo.Interactions.disable_interaction(poll) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, nil}
          )
        end

        {:noreply,
         socket
         |> assign(:current_interaction, nil)
         |> interactions_at_position(socket.assigns.state.position)}
    end
  end

  @impl true
  def handle_event("form-set-inactive", %{"id" => id}, socket) do
    case Forms.get_form_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      form ->
        with {:ok, _} <- Bludo.Interactions.disable_interaction(form) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, nil}
          )
        end

        {:noreply,
         socket
         |> assign(:current_interaction, nil)
         |> interactions_at_position(socket.assigns.state.position)}
    end
  end

  @impl true
  def handle_event("embed-set-inactive", %{"id" => id}, socket) do
    case Embeds.get_embed_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      embed ->
        with {:ok, _} <- Bludo.Interactions.disable_interaction(embed) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, nil}
          )
        end

        {:noreply,
         socket
         |> assign(:current_interaction, nil)
         |> interactions_at_position(socket.assigns.state.position)}
    end
  end

  @impl true
  def handle_event("quiz-set-active", %{"id" => id}, socket) do
    case Quizzes.get_quiz_for_event(id, event_id(socket), [
           :quiz_questions,
           quiz_questions: :quiz_question_opts
         ]) do
      nil ->
        {:noreply, socket}

      quiz ->
        with :ok <- Bludo.Interactions.enable_interaction(quiz) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, quiz}
          )

          {:noreply,
           socket
           |> assign(:current_interaction, quiz)
           |> interactions_at_position(socket.assigns.state.position)}
        end
    end
  end

  @impl true
  def handle_event("quiz-set-inactive", %{"id" => id}, socket) do
    case Quizzes.get_quiz_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      quiz ->
        with {:ok, _} <- Bludo.Interactions.disable_interaction(quiz) do
          Phoenix.PubSub.broadcast(
            Bludo.PubSub,
            "event:#{socket.assigns.event.uuid}",
            {:current_interaction, nil}
          )
        end

        {:noreply,
         socket
         |> assign(:current_interaction, nil)
         |> interactions_at_position(socket.assigns.state.position)}
    end
  end

  @impl true
  def handle_event("interaction-enable", %{"id" => id, "type" => type}, socket) do
    case type do
      "poll" -> handle_event("poll-set-active", %{"id" => id}, socket)
      "form" -> handle_event("form-set-active", %{"id" => id}, socket)
      "quiz" -> handle_event("quiz-set-active", %{"id" => id}, socket)
      _ -> {:noreply, socket}
    end
  end

  @impl true
  def handle_event("interaction-disable", %{"id" => id, "type" => type}, socket) do
    case type do
      "poll" -> handle_event("poll-set-inactive", %{"id" => id}, socket)
      "form" -> handle_event("form-set-inactive", %{"id" => id}, socket)
      "quiz" -> handle_event("quiz-set-inactive", %{"id" => id}, socket)
      _ -> {:noreply, socket}
    end
  end

  @impl true
  def handle_event("pin", %{"id" => id}, socket) do
    case Bludo.Posts.get_post_for_event(id, event_id(socket), [:event]) do
      nil -> {:noreply, socket}
      post -> pin(post, socket)
    end
  end

  @impl true
  def handle_event("pin-post", %{"id" => id}, socket) do
    post = Bludo.Posts.get_post!(id)
    {:ok, _} = Bludo.Posts.toggle_pin_post(post)
    {:noreply, socket}
  end

  @impl true
  def handle_event("unpin-post", %{"id" => id}, socket) do
    post = Bludo.Posts.get_post!(id)
    {:ok, _} = Bludo.Posts.toggle_pin_post(post)
    {:noreply, socket}
  end

  @impl true
  def handle_event("delete", %{"id" => id}, socket) do
    case Bludo.Posts.get_post_for_event(id, event_id(socket), [:event]) do
      nil ->
        {:noreply, socket}

      post ->
        {:ok, _} = Bludo.Posts.delete_post(post)

        updated_socket =
          if post.pinned do
            stream(socket, :pinned_posts, list_pinned_posts(socket, socket.assigns.event.uuid),
              reset: true
            )

            stream(socket, :posts, list_all_posts(socket, socket.assigns.event.uuid), reset: true)
          else
            stream(socket, :posts, list_all_posts(socket, socket.assigns.event.uuid), reset: true)
          end

        {:noreply, updated_socket}
    end
  end

  @impl true
  def handle_event("delete-post", %{"id" => id}, socket) do
    post = Bludo.Posts.get_post!(id)
    {:ok, _} = Bludo.Posts.delete_post(post)
    {:noreply, socket}
  end

  @impl true
  def handle_event("delete-poll", %{"id" => id}, socket) do
    case Polls.get_poll_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      poll ->
        {:ok, _} = Polls.delete_poll(socket.assigns.event.uuid, poll)
        {:noreply, socket}
    end
  end

  @impl true
  def handle_event("delete-quiz", %{"id" => id}, socket) do
    case Quizzes.get_quiz_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      quiz ->
        {:ok, _} = Quizzes.delete_quiz(socket.assigns.event.uuid, quiz)
        {:noreply, socket}
    end
  end

  @impl true
  def handle_event("delete-form-submit", %{"id" => id}, socket) do
    case Bludo.Forms.get_form_submit_for_event(id, event_id(socket)) do
      nil ->
        {:noreply, socket}

      form_submit ->
        {:ok, _} = Bludo.Forms.delete_form_submit(socket.assigns.event.uuid, form_submit)

        {:noreply,
         assign(
           socket,
           :form_submits,
           list_form_submits(socket, socket.assigns.event.presentation_file.id)
         )}
    end
  end

  @impl true
  def handle_event("ban", %{"attendee-identifier" => attendee_identifier}, socket) do
    Bludo.Posts.delete_all_posts(:attendee_identifier, attendee_identifier, socket.assigns.event)
    ban(attendee_identifier, socket)
  end

  @impl true
  def handle_event("ban", %{"user-id" => user_id}, socket) do
    Bludo.Posts.delete_all_posts(:user_id, user_id, socket.assigns.event)
    ban(String.to_integer(user_id), socket)
  end

  @impl true
  def handle_event("ban-user", %{"id" => id}, socket) do
    ban(id, socket)
  end

  @impl true
  def handle_event("checked", %{"key" => "review_quiz_questions"}, socket) do
    Phoenix.PubSub.broadcast(
      Bludo.PubSub,
      "event:#{socket.assigns.event.uuid}",
      {:review_quiz_questions}
    )

    {:noreply, socket}
  end

  @impl true
  def handle_event("checked", %{"key" => "next_quiz_question"}, socket) do
    Phoenix.PubSub.broadcast(
      Bludo.PubSub,
      "event:#{socket.assigns.event.uuid}",
      {:next_quiz_question}
    )

    {:noreply, socket}
  end

  @impl true
  def handle_event("checked", %{"key" => "prev_quiz_question"}, socket) do
    Phoenix.PubSub.broadcast(
      Bludo.PubSub,
      "event:#{socket.assigns.event.uuid}",
      {:prev_quiz_question}
    )

    {:noreply, socket}
  end

  @impl true
  def handle_event("checked", %{"key" => "quiz_show_results", "value" => value}, socket) do
    {:ok, new_interaction} =
      Bludo.Quizzes.update_quiz(
        socket.assigns.event.uuid,
        socket.assigns.current_interaction,
        %{
          :show_results => value
        }
      )

    {:noreply, socket |> assign(:current_interaction, new_interaction)}
  end

  @impl true
  def handle_event("checked", %{"key" => key, "value" => value}, socket) do
    key_atom = String.to_existing_atom(key)

    {:ok, new_state} =
      Bludo.Presentations.update_presentation_state(
        socket.assigns.state,
        %{
          key_atom => value
        }
      )

    {:noreply, socket |> assign(:state, new_state)}
  end

  @impl true
  def handle_event("sort-questions", %{"sort" => sort}, socket) do
    {:noreply,
     socket
     |> assign(:sort_questions_by, sort)
     |> stream(:questions, list_all_questions(socket, socket.assigns.event.uuid, sort),
       reset: true
     )}
  end

  @impl true
  def handle_event("list-tab", %{"tab" => tab}, socket) do
    {tab_atom, socket} =
      case tab do
        "posts" ->
          {:posts,
           stream(socket, :posts, list_all_posts(socket, socket.assigns.event.uuid), reset: true)}

        "questions" ->
          {:questions,
           stream(socket, :questions, list_all_questions(socket, socket.assigns.event.uuid),
             reset: true
           )}

        "forms" ->
          {:forms,
           stream(
             socket,
             :form_submits,
             list_form_submits(socket, socket.assigns.event.presentation_file.id),
             reset: true
           )}

        "pinned_posts" ->
          {:pinned_posts,
           stream(
             socket,
             :pinned_posts,
             list_pinned_posts(socket, socket.assigns.event.uuid),
             reset: true
           )}

        _ ->
          {:posts,
           stream(socket, :posts, list_all_posts(socket, socket.assigns.event.uuid), reset: true)}
      end

    {:noreply, assign(socket, :list_tab, tab_atom)}
  end

  @impl true
  def handle_event("maybe-redirect", _params, socket) do
    if socket.assigns.create != nil do
      {:noreply,
       socket
       |> push_navigate(to: ~p"/e/#{socket.assigns.event.code}/manage")}
    else
      {:noreply, socket}
    end
  end

  @impl true
  def handle_event("toggle-preview", _params, socket) do
    {:noreply, socket |> update(:preview, &(!&1))}
  end

  @impl true
  def handle_event("toggle-interaction-modal", _params, socket) do
    socket =
      socket
      |> update(:interaction_modal, &(!&1))
      |> assign(:create, nil)

    if not socket.assigns.interaction_modal do
      {:noreply, push_patch(socket, to: ~p"/e/#{socket.assigns.event.code}/manage")}
    else
      {:noreply, socket}
    end
  end

  @impl true
  def handle_event("toggle-chat", _params, socket) do
    {:ok, new_state} =
      Bludo.Presentations.update_presentation_state(socket.assigns.state, %{
        :chat_visible => !socket.assigns.state.chat_visible
      })

    {:noreply, socket |> assign(:state, new_state)}
  end

  @impl true
  def handle_params(params, _url, socket) do
    {:noreply, apply_action(socket, socket.assigns.live_action, params)}
  end

  # --- PRIVATE HELPERS ---

  def toggle_settings_modal(js \\ %JS{}) do
    js
    |> JS.toggle(
      to: "#settings-modal",
      out: "animate__animated animate__fadeOut",
      in: "animate__animated animate__fadeIn"
    )
  end

  defp apply_action(socket, :show, _params) do
    socket
  end

  defp apply_action(socket, :add_presentation, _params) do
    socket
    |> assign(:create, "presentation")
    |> assign(:interaction_modal, true)
  end

  defp apply_action(socket, :add_poll, _params) do
    socket
    |> assign(:create, "poll")
    |> assign(:interaction_modal, true)
    |> assign(:poll, %Polls.Poll{
      poll_opts: [%Polls.PollOpt{content: gettext("Yes")}, %Polls.PollOpt{content: gettext("No")}]
    })
  end

  defp apply_action(socket, :edit_poll, %{"id" => id}) do
    case Polls.get_poll_for_event(id, event_id(socket)) do
      nil ->
        socket
        |> put_flash(:error, gettext("Resource not found"))
        |> push_navigate(to: ~p"/e/#{socket.assigns.event.code}/manage")

      poll ->
        socket
        |> assign(:create, "poll")
        |> assign(:interaction_modal, true)
        |> assign(:create_action, :edit)
        |> assign(:poll, poll)
    end
  end

  defp apply_action(socket, :add_form, _params) do
    socket
    |> assign(:create, "form")
    |> assign(:interaction_modal, true)
    |> assign(:form, %Forms.Form{
      fields: [
        %Forms.Field{name: gettext("Name"), type: "text"},
        %Forms.Field{name: gettext("Email"), type: "email"}
      ]
    })
  end

  defp apply_action(socket, :add_embed, _params) do
    socket
    |> assign(:create, "embed")
    |> assign(:embed, %Embeds.Embed{})
  end

  defp apply_action(socket, :import, _params) do
    socket
    |> assign(:create, "import")
    |> assign(:events, Bludo.Events.list_events(socket.assigns.current_user.id))
  end

  defp apply_action(socket, :edit_form, %{"id" => id}) do
    case Forms.get_form_for_event(id, event_id(socket)) do
      nil ->
        socket
        |> put_flash(:error, gettext("Resource not found"))
        |> push_navigate(to: ~p"/e/#{socket.assigns.event.code}/manage")

      form ->
        socket
        |> assign(:create, "form")
        |> assign(:interaction_modal, true)
        |> assign(:create_action, :edit)
        |> assign(:form, form)
    end
  end

  defp apply_action(socket, :edit_embed, %{"id" => id}) do
    case Embeds.get_embed_for_event(id, event_id(socket)) do
      nil ->
        socket
        |> put_flash(:error, gettext("Resource not found"))
        |> push_navigate(to: ~p"/e/#{socket.assigns.event.code}/manage")

      embed ->
        socket
        |> assign(:create, "embed")
        |> assign(:interaction_modal, true)
        |> assign(:create_action, :edit)
        |> assign(:embed, embed)
    end
  end

  defp apply_action(socket, :add_quiz, _params) do
    socket
    |> assign(:create, "quiz")
    |> assign(:interaction_modal, true)
    |> assign(:quiz, %Quizzes.Quiz{
      presentation_file_id: socket.assigns.event.presentation_file.id,
      quiz_questions: [
        %Quizzes.QuizQuestion{
          id: 0,
          quiz_question_opts: [
            %Quizzes.QuizQuestionOpt{
              id: 0
            },
            %Quizzes.QuizQuestionOpt{
              id: 1
            }
          ]
        }
      ]
    })
  end

  defp apply_action(socket, :edit_quiz, %{"id" => id}) do
    case Quizzes.get_quiz_for_event(id, event_id(socket), [
           :quiz_questions,
           quiz_questions: :quiz_question_opts
         ]) do
      nil ->
        socket
        |> put_flash(:error, gettext("Resource not found"))
        |> push_navigate(to: ~p"/e/#{socket.assigns.event.code}/manage")

      quiz ->
        socket
        |> assign(:create, "quiz")
        |> assign(:interaction_modal, true)
        |> assign(:create_action, :edit)
        |> assign(:quiz, quiz)
    end
  end

  defp pin(post, socket) do
    {:ok, _updated_post} = Bludo.Posts.toggle_pin_post(post)

    {:noreply, socket}
  end

  defp ban(user, %{assigns: %{event: event, state: state}} = socket) do
    {:ok, new_state} =
      Bludo.Presentations.update_presentation_state(state, %{
        "banned" => state.banned ++ ["#{user}"]
      })

    Phoenix.PubSub.broadcast(
      Bludo.PubSub,
      "event:#{event.uuid}",
      {:banned, user}
    )

    {:noreply, socket |> assign(:state, new_state)}
  end

  defp interaction_type(%Bludo.Polls.Poll{}), do: "poll"
  defp interaction_type(%Bludo.Forms.Form{}), do: "form"
  defp interaction_type(%Bludo.Quizzes.Quiz{}), do: "quiz"
  defp interaction_type(%Bludo.Embeds.Embed{}), do: "embed"
  defp interaction_type(_), do: "unknown"

  defp interactions_at_position(
         %{assigns: %{event: event}} = socket,
         position,
         broadcast \\ false
       ) do
    with {:ok, interactions} <-
           Bludo.Interactions.get_interactions_at_position(event, position, broadcast) do
      active = interactions |> Enum.find(& &1.enabled)
      socket |> assign(:interactions, interactions) |> assign(:current_interaction, active)
    end
  end

  defp list_pinned_posts(_socket, event_id) do
    Bludo.Posts.list_pinned_posts(event_id, [:event, :reactions])
  end

  defp list_all_posts(_socket, event_id) do
    Bludo.Posts.list_posts(event_id, [:event, :reactions])
  end

  defp list_all_questions(_socket, event_id, sort \\ "date") do
    sort_atom =
      case sort do
        "likes" -> :likes
        _ -> :date
      end

    Bludo.Posts.list_questions(event_id, [:event, :reactions], sort_atom)
    |> Enum.filter(&(BludoWeb.Helpers.body_without_links(&1.body) =~ "?"))
  end

  defp list_form_submits(_socket, presentation_file_id) do
    Bludo.Forms.list_form_submits(presentation_file_id, [:form])
  end
end
